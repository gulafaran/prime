pkgname=prime-git
pkgver=0.0.1_r11.g2034bd6
pkgrel=1
pkgdesc="tool to help run on nvidia dgpus in laptops on linux"
arch=($CARCH)
url="https://github.com/gulafaran/prime"

depends=(vulkan-headers glfw glew glibc qt5-base)
makedepends=(git cmake gcc)
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
source=("git+https://github.com/gulafaran/prime")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname%-git}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
